math := import("math")
vec3 := import("vec3")
color := import("color")
rand := import("rand")

s := import("shape")
m := import("material")

// This scene was made to test the speed up from BVH

grid_size := 10
sphere_radius := 4
sphere_spacing := 4

// Distance between two centers
grid_stride := (sphere_radius *2) + sphere_spacing
// Width from center to center
grid_width := grid_stride * (grid_size - 1)
grid_start := -(grid_width/2)

export {
    config: {
        aspect_ratio: 1.0,
        image_width: 800,
        samples: 500,
        depth: 100
    },

    scene: func(se, frame, seconds) {
        for i := 0; i < grid_size; i++ {
            for j := 0; j < grid_size; j++ {
                for k := 0; k < grid_size; k++ {
                    se.Object(
                        s.Sphere(vec3.New(
                            grid_start + (i * grid_stride),
                            grid_start + (j * grid_stride),
                            grid_start + (k * grid_stride)
                        ), sphere_radius),
                        m.Metal(color.Random(0.7), 0.5)
                    )
                }
            }
        }

        se.Camera(
            vec3.New(100, 200, 300),
            vec3.New(0, 0, 0),
            20
        )
    }
}